# Copyright (C) 2021-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
import("//foundation/filemanagement/dfs_service/distributedfile.gni")

ohos_shared_library("libdistributedfiledaemon") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  include_dirs = [
    "include",
    "include/network/softbus",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/include",
    "${distributedfile_path}/services/distributedfiledaemon/include/dfx",
    "${distributedfile_path}/services/distributedfiledaemon/include/ipc",
    "${distributedfile_path}/services/distributedfiledaemon/include/channel_manager",
    "${distributedfile_path}/utils/dfx/include",
  ]

  cflags = [ "-D_FORTIFY_SOURCE=2" ]

  sources = [
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/asset/asset_obj.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/distributed_file_fd_guard.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/file_copy_listener.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/file_copy_manager.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/file_size_utils.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/ipc_wrapper.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/trans_listener.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/remote_file_copy_manager.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/distributed_file_daemon_proxy.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/file_trans_listener_stub.cpp",
    "src/all_connect/all_connect_manager.cpp",
    "src/channel_manager/channel_manager.cpp",
    "src/channel_manager/control_cmd_parser.cpp",
    "src/channel_manager/system_notifier.cpp",
    "src/connect_count/connect_count.cpp",
    "src/device/device_info.cpp",
    "src/device/device_manager_agent.cpp",
    "src/device/device_profile_adapter.cpp",
    "src/dfx/radar_report.cpp",
    "src/ipc/asset_callback_manager.cpp",
    "src/ipc/asset_recv_callback_proxy.cpp",
    "src/ipc/asset_send_callback_proxy.cpp",
    "src/ipc/connection_detector.cpp",
    "src/ipc/daemon.cpp",
    "src/ipc/daemon_eventhandler.cpp",
    "src/ipc/daemon_execute.cpp",
    "src/ipc/daemon_stub.cpp",
    "src/ipc/file_dfs_listener_proxy.cpp",
    "src/ipc/file_trans_listener_proxy.cpp",
    "src/ipc/trans_mananger.cpp",
    "src/mountpoint/mount_manager.cpp",
    "src/mountpoint/mount_point.cpp",
    "src/multiuser/os_account_observer.cpp",
    "src/network/devsl_dispatcher.cpp",
    "src/network/kernel_talker.cpp",
    "src/network/network_agent_template.cpp",
    "src/network/session_pool.cpp",
    "src/network/softbus/softbus_agent.cpp",
    "src/network/softbus/softbus_asset_recv_listener.cpp",
    "src/network/softbus/softbus_asset_send_listener.cpp",
    "src/network/softbus/softbus_file_receive_listener.cpp",
    "src/network/softbus/softbus_file_send_listener.cpp",
    "src/network/softbus/softbus_handler.cpp",
    "src/network/softbus/softbus_handler_asset.cpp",
    "src/network/softbus/softbus_permission_check.cpp",
    "src/network/softbus/softbus_session.cpp",
    "src/network/softbus/softbus_session_dispatcher.cpp",
    "src/network/softbus/softbus_session_listener.cpp",
    "src/network/softbus/softbus_session_pool.cpp",
  ]

  deps = [
    "${utils_path}:libdistributedfileutils",
  ]

  external_deps = [
    "app_file_service:fileuri_native",
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:abilitykit_native",
    "ability_runtime:uri_permission_mgr",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "app_file_service:remote_file_share_native",
    "app_file_service:sandbox_helper_native",
    "cJSON:cjson_static",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "data_share:datashare_consumer",
    "device_auth:deviceauth_sdk",
    "eventhandler:libeventhandler",
    "file_api:securitylabel",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "init:libbegetutil",
    "ipc:ipc_single",
    "json:nlohmann_json_static",
    "os_account:libaccountkits",
    "os_account:os_account_innerkits",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "storage_service:storage_manager_sa_proxy",
    "zlib:shared_libz",
  ]

  configs = [ "${utils_path}:compiler_configs" ]

  defines = [
    "LOG_DOMAIN=0xD004315",
    "LOG_TAG=\"distributedfile_daemon\"",
  ]

  if (support_same_account) {
    defines += [ "SUPPORT_SAME_ACCOUNT" ]
  }


  if (dfs_service_feature_enable_distributed_ability) {
    external_deps += [
      "dataclassification:data_transit_mgr",
      "device_manager:devicemanagersdk",
      "dsoftbus:softbus_client",
    ]
    defines += [ "DFS_ENABLE_DISTRIBUTED_ABILITY" ]
  }

  if (support_device_profile) {
    sources += [ 
      "src/device/dfs_static_capability.cpp" 
    ]

    external_deps += [
      "app_file_service:remote_file_share_native",
      "device_info_manager:distributed_device_profile_common",
      "device_info_manager:distributed_device_profile_sdk",
    ]

    defines += [ "SUPPORT_DEVICE_PROFILE" ]
  }

  part_name = "dfs_service"
  subsystem_name = "filemanagement"
}

config("public_config") {
  include_dirs = [
    ".",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/include",
    "${distributedfile_path}/services/distributedfiledaemon/include/ipc",
  ]
}

config("private_config") {
  include_dirs = [
    "${distributedfile_path}/frameworks/native/distributed_file_inner/include",
    "${distributedfile_path}/utils/dfx/include",
    "${distributedfile_path}/utils/log/include",
    "${services_path}/distributedfiledaemon/include",
  ]
}

ohos_shared_library("distributed_file_daemon_kit_inner") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  sources = [
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/distributed_file_fd_guard.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/file_copy_listener.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/file_copy_manager.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/file_size_utils.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/distributed_file_daemon_manager.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/distributed_file_daemon_manager_impl.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/file_dfs_listener_stub.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/file_mount_manager.cpp",
    "${distributedfile_path}/frameworks/native/distributed_file_inner/src/file_trans_listener_stub.cpp",
    "${distributedfile_path}/utils/log/src/utils_log.cpp",
  ]

  defines = [
    "LOG_DOMAIN=0xD004315",
    "LOG_TAG=\"distributedfile_daemon\"",
  ]
  public_external_deps = []

  deps = [ "${utils_path}:libdistributedfileutils" ]

  external_deps = [
    "ability_runtime:abilitykit_native",
    "access_token:libaccesstoken_sdk",
    "app_file_service:fileuri_native",
    "app_file_service:remote_file_share_native",
    "c_utils:utils",
    "data_share:datashare_consumer",
    "hilog:libhilog",
    "ipc:ipc_single",
  ]

  if (dfs_service_feature_enable_distributed_ability) {
    sources += [
      "${distributedfile_path}/frameworks/native/distributed_file_inner/src/asset/asset_adapter_sa_client.cpp",
      "${distributedfile_path}/frameworks/native/distributed_file_inner/src/asset/asset_obj.cpp",
      "${distributedfile_path}/frameworks/native/distributed_file_inner/src/asset/asset_recv_callback_stub.cpp",
      "${distributedfile_path}/frameworks/native/distributed_file_inner/src/asset/asset_send_callback_stub.cpp",
      "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/ipc_wrapper.cpp",
      "${distributedfile_path}/frameworks/native/distributed_file_inner/src/copy/trans_listener.cpp",
      "${distributedfile_path}/frameworks/native/distributed_file_inner/src/distributed_file_daemon_proxy.cpp",
    ]
    external_deps += [
      "safwk:system_ability_fwk",
      "storage_service:storage_manager_sa_proxy",
      "samgr:samgr_proxy",
      "dsoftbus:softbus_client",
    ]
    public_external_deps += [ "device_manager:devicemanagersdk" ]
    defines += [ "DFS_ENABLE_DISTRIBUTED_ABILITY" ]
  }

  configs = [ ":private_config" ]
  public_configs = [ ":public_config" ]
  cflags = [ "-D_FORTIFY_SOURCE=2" ]
  use_exceptions = true
  part_name = "dfs_service"
  subsystem_name = "filemanagement"
}
