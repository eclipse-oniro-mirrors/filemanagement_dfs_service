# Copyright (C) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/filemanagement/dfs_service/distributedfile.gni")

group("cloud_adapter_example_target") {
  deps = []
  if (!dfs_service_feature_enable_cloud_adapter) {
    deps += [
      "${distributedfile_path}/adapter/cloud_adapter_example:cloud_adapter",
    ]
  }
}

config("public_config") {
  include_dirs =
      [ "${distributedfile_path}/adapter/cloud_adapter_example/include" ]
}

config("optimize-size") {
  cflags = [
    "-fvisibility=hidden",
    "-fdata-sections",
    "-ffunction-sections",
    "-Oz",
  ]
  cflags_cc = [
    "-fvisibility-inlines-hidden",
    "-Oz",
  ]
}

ohos_shared_library("cloud_adapter") {
  branch_protector_ret = "pac_ret"
  configs = [ ":optimize-size" ]
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  sources = [
    "${distributedfile_path}/adapter/cloud_adapter_example/src/cloud_asset_read_session_impl.cpp",
    "${distributedfile_path}/adapter/cloud_adapter_example/src/cloud_assets_downloader_impl.cpp",
    "${distributedfile_path}/adapter/cloud_adapter_example/src/cloud_database_impl.cpp",
    "${distributedfile_path}/adapter/cloud_adapter_example/src/cloud_file_kit_impl.cpp",
    "${distributedfile_path}/adapter/cloud_adapter_example/src/cloud_sync_helper_impl.cpp",
    "${distributedfile_path}/adapter/cloud_adapter_example/src/data_sync_manager_impl.cpp",
  ]

  public_configs = [ ":public_config" ]

  deps = [
    "${innerkits_native_path}/cloud_file_kit_inner:cloudfile_kit",
    "${utils_path}:libdistributedfileutils",
  ]

  use_exceptions = true
  part_name = "dfs_service"
  subsystem_name = "filemanagement"
}
